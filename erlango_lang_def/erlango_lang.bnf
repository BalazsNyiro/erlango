# read this: "https://www.erlang.org/doc/system/data_types.html#atom"
##################################################################

# TODO for later: add try/catch structure for error handling


# ========== expression   =====================
<program> ::= <dashPrgAttrib> <program>
            | <functionDefinition> <programTail>

# -module("abc")
<dashPrgAttrib> ::= "-" <atom> "(" <dashArg> ")" "."
<dashArg> ::= <string>
            | <exportList>

<programTail> ::= <functionDefinition> <programTail>
                | <empty>

<exportList> ::= "[" <exportEntryList> "]"

<exportEntryList> ::= <exportEntry> <exportEntryListTail>
                    | <empty>

<exportEntryListTail> ::= "," <exportEntry> <exportEntryListTail>
                        | <empty>

<exportEntry> ::= <atom> "/" <integer>




# ========== expression   =====================

<functionDefinition> ::= <functionName> "(" <parameterList> ")" "->" <expression> "."

<functionName> ::= <atom>


<parameterList> ::= <atom_or_variable_name_in_paramlist> <parameterListTail>
                  | <empty>
<parameterListTail> ::= "," <atom_or_variable_name_in_paramlist> <parameterListTail>
                      | <empty>
<atom_or_variable_name_in_paramlist> ::= <atom> | <variable>



<expression> ::= <expression> "+" <term>
               | <expression> "-" <term>
               | <expression> "=" <term>
               | <expression> "==" <term>
               | <expression> "/=" <term>
               | <expression> "=:=" <term>
               | <expression> "=/=" <term>
               | <expression> "<" <term>
               | <expression> "=<" <term>
               | <expression> ">" <term>
               | <expression> ">=" <term>
               | <expression> "++" <term>
               | <expression> "--" <term>
               | <expression> "!" <term>
               | <term>

<term> ::= <term> "*" <factor>
         | <term> "/" <factor>
         | <factor>

<factor> ::= <number>
           | <string>
           | <atom>
           | <variable>
           | <functionCall>
           | "(" <expression> ")"
           | <caseExpression>
           | <list>
           | <map>
           | <pid>





# ======  FUNCTIONS ========
<functionCall> ::= <functionName> "(" <argumentList> ")"

<argumentList> ::= <expression> <argumentListTail>
                 | <empty>

<argumentListTail> ::= "," <expression> <argumentListTail>
                     | <empty>

# ========== CASE =====================
<caseExpression> ::= "case" <expression> "of" <caseClauseList> "end"

<caseClauseList> ::= <caseClause> <caseClauseTail>

<caseClauseTail> ::= <caseClause> <caseClauseTail>
                   | <empty>

<caseClause> ::= <pattern> "->" <expression> ","
               | <pattern> "->" <expression>

<pattern> ::= <variable>
            | <number>
            | <string>
            | <atom>
            | <list>
            | <tuple>
            | <map>
            | <pid>


# ========== erlang_types_=====================
<tuple> ::= "{" <expressionList> "}"
<list> ::= "[" <expressionList> "]"

<expressionList> ::= <expression> <expressionListTail>
                   | <empty>

<expressionListTail> ::= "," <expression> <expressionListTail>
                       | <empty>

<map> ::= "#{" <mapEntryList> "}"

<mapEntryList> ::= <mapEntry> <mapEntryListTail>
                 | <empty>

<mapEntryListTail> ::= "," <mapEntry> <mapEntryListTail>
                     | <empty>

<mapEntry> ::= <expression> "=>" <expression>

<pid> ::= "<" <integer> "." <integer> "." <integer> ">"



<string> ::= <stringQuoteOne> | <stringInSigil>
<stringQuoteOne> ::= "\"" <stringQuoteOneTail> "\""
<stringQuoteOneTail> ::=  <anyUnicodeCharsExceptNonEscapedSingleQuote> | <empty>

<stringInSigil> ::= "\"\"\"" <stringSigilTail> "\"\"\""
<stringSigilTail> ::=  <anyUnicodeChars> | <empty>



<atom> ::= <atomInQuotes> | <atomSmallFirstChar>
<atomSmallFirstChar> ::= <letterSmall> <atomSmallFirstChar_tail>
<atomSmallFirstChar_tail> ::= <atomPossibleCharAfterFirstPosition> <atomSmallFirstChar_tail>
                            | <empty>

<atomInQuotes> ::= "'" <atomCharList_inQuotes> "'"
<atomCharList_inQuotes> ::= <anyUnicodeChars> <atomCharList_inQuotes>
                          | <empty>


<number> ::= <integer> | <float>
<integer> ::= <digit> <numberTail>
<float> ::= <digit> <numberTail> "." <digit> <numberTail>

<numberTail> ::= <digit> <numberTail>
               | <empty>



<variable> ::= <variableCapitalLetterOrUnderscore> <variableTail>
<variableCapitalLetterOrUnderscore> ::= <letterCapital>
                                      | "_"
<variableTail> ::= <variableLetterOrDigitOrUnderscore> <variableTail>
                 | <empty>
<variableLetterOrDigitOrUnderscore> ::= <letterSmallCapital>
                                      | <digit>
                                      | "_"



# ===============_comment_section_abc_=====================
# TODO: all unicode char has to be accepted, except , handle this in the parser
<anyUnicodeChars> ::= <atomPossibleCharAfterFirstPosition>

# TODO: all unicode char has to be accepted, handle this in the parser
<anyUnicodeCharsExceptNonEscapedSingleQuote> ::= <atomPossibleCharAfterFirstPosition>





<escapeSequence> ::= "\\" <escapeCharInSeq>
<escapeCharInSeq> ::= "n" | "t" | "\\" | "\"" | "'"

<atomPossibleCharAfterFirstPosition> ::= <letterSmallCapital>
                                      | <digit>
                                      | "_" | "@"






<letterSmallCapital> ::= <letterSmall>
                       | <letterCapital>

<letterSmall> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j"
                | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t"
                | "u" | "v" | "w" | "x" | "y" | "z"

<letterCapital> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J"
                  | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T"
                  | "U" | "V" | "W" | "X" | "Y" | "Z"

<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<empty> ::=   /* nothing */
